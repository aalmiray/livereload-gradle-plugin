/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2013-2020 Andres Almiray.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'java-library'
    id 'groovy'
    id 'org.kordamp.gradle.groovy-project'
    id 'org.kordamp.gradle.bintray'
    id 'org.kordamp.gradle.plugin'
}

config {
    info {
        name          = rootProject.name
        description   = 'LiveReload Gradle Plugin'
        inceptionYear = '2013'
        tags          = ['jandex']

        specification { enabled = false }
    }

    docs {
        groovydoc {
            replaceJavadoc = true
        }
    }

    plugins {
        plugin {
            name                = 'jandex'
            id                  = 'org.kordamp.gradle.livereload'
            implementationClass = 'org.kordamp.gradle.plugin.livereload.LiveReloadPlugin'
        }
    }

    bintray {
        skipMavenSync = true
    }
}

repositories {
    gradlePluginPortal()
    mavenLocal()
}

dependencies {
    compileOnly gradleApi()

    api "org.kordamp.gradle:base-gradle-plugin:$kordampPluginVersion"
    api 'net.alchim31:livereload-jvm:0.2.0'
    testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        exclude group: 'junit', module: 'junit'
    }
    testImplementation 'cglib:cglib-nodep:3.3.0'
    testImplementation 'org.objenesis:objenesis:3.1'
}

processResources {
    inputs.property('version', project.version)
    filesMatching(['**/*.properties']) {
        expand(
            'version': project.version,
            'id'     : 'livereload',
            'name'   : 'LiveReload Gradle plugin'
        )
    }
}